recursive call
-----
_ do [
    .f set _ function {
        code : (__ : .i) : .i,
    },
    _ f _ not true
]
-----
false
=====
call external func
-----
_ do [
    .f set _ function {
        code : (__ : .i) : _ form _ not .i
    },
    _ f _ not true
]
-----
true
=====
call internal func
-----
_ do [
    .memory set _ import _memory.new,
    .f set _ function {
        code : (__ : .i) : _ form _ .not .i,
        memory : _ memory {
            not : . : not
        },
    },
    _ f _ not true
]
-----
true
=====
call rename
-----
_ do [
    .memory set _ import _memory.new,
    .f set _ function {
        code : (__ : .a) : _ form _ .n .a,
        memory : _ memory {
            n : . : not
        },
    },
    _ f _ not true
]
-----
true
=====
stateful
-----
_ do [
    .memory set _ import _memory.new,
    .new_link set _ import _link.new,
    .link_get set _ import _link.get,
    .link_set set _ import _link.set,
    .f set _ function {
        context_access : _free,
        code : __ : _ form _ do [
            ..i set .a link_get _,
            .a link_set .i + 1,
            .i
        ],
        memory : _ memory {
            a : . : _ new_link 0
        },
    },
    [_ f ., _ f ., _ f .]
]
-----
[0, 1, 2]
=====
multiple stateful
-----
_ do [
    .memory set _ import _memory.new,
    .new_link set _ import _link.new,
    .link_get set _ import _link.get,
    .link_set set _ import _link.set,
    .f set _ data _ function {
        context_access : _free,
        code : __ : _ form _ do [
            ..i set .a link_get _,
            .a link_set .i + 1,
            .i
        ],
        memory : _ memory {
            a : . : _ new_link 0
        },
    },
    .f1 set _ eval f,
    .f2 set _ eval f,
    [_ f1 ., _ f1 ., _ f1 ., _ f2 ., _ f2 ., _ f2 .]
]
-----
[0, 1, 2, 0, 1, 2]
=====
const const
-----
_ do [
    _a set 1,
    .f set _ function {
        code : (_c : __) : _ form _c which _ get _a,
        context_access : _constant,
    },
    _ f .
]
-----
1
=====
mutable const
-----
_ do [
    _a set 1,
    .f set _ function {
        code : (_c : __) : _ form _c which _ get _a,
        context_access : _mutable,
    },
    _ f .
]
-----
1
=====
mutable mutate
-----
_ do [
    .f set _ function {
        code : (_c : .i) : _ form _c which .i set 2,
        context_access : _mutable,
    },
    .a set 1,
    _ f .a,
    a
]
-----
2
=====
const mutate fail
-----
_ do [
    .f set _ function {
        code : (_c : .i) : _ form _c which .i set 2,
        context_access : _constant,
    },
    .a set 1,
    _ f .a,
    a
]
-----
1
=====
const free
-----
_ do [
    .a set .,
    .f set _ function {
        code : (__c : __) : 1,
        context_access : _constant,
    },
    a which _ f .
]
-----
1
=====
ctx name occupied
-----
_ do [
    .memory set _ import _memory.new,
    .a set 1,
    .f set _ function {
        code : (_ctx : __) : 2,
        context_access : _constant,
        memory : _ memory {
            ctx : _constant : 3
        },
    },
    _ f .,
    a == 1
]
-----
true
=====
ctx panic safe
-----
_ do [
    .limit_steps set _ import _resource.limit_steps,
    .f set _ function {
        context_access : _constant,
        code : (_ctx : __) : _ form _ do [
            [1, 2, 3, 4, 5, 6, 7, 8, 9, 10],
            11
        ],
    },
    .p set 1 : 2,
    10 limit_steps _ data p which _ f .,
    p
]
-----
1 : 2
=====
ctx explicit
-----
_ do [
    .set_first set _ import _pair.set_first,
    .a set 1 : 2,
    .f set _ function {
        code : (_p : .i) : _ form _p which _ set_first .i,
    },
    a which _ f 3,
    a
]
-----
3 : 2
=====
repr
-----
_ do [
    .represent set _ import _function.represent,
    _ represent _ function {
        code : (.c : .i) : .i
    }
]

-----
{code : (c : i) : i}
=====
repr primitive free
-----
_ do [
    .represent set _ import _function.represent,
    _ represent not
]
-----
{id : _bit.not}
=====
repr const
-----
_ do [
    .represent set _ import _function.represent,
    _ represent _ function {
        code : (_c : .i) : .i,
        context_access : _constant
    }
]
-----
{code : (_c : i) : i, context_access : _constant}
=====
repr primitive const
-----
_ do [
    .represent set _ import _function.represent,
    _ represent ==
]
-----
{id : _value.equal}
=====
repr composite
-----
_ do [
    .represent set _ import _function.represent,
    .repr set {
        id : _f,
        code : (_ctx : .in) : .in,
        context_access : _constant,
    },
    .new_repr set _ represent _ function repr,
    repr == new_repr
]
-----
true
=====
access free
-----
_ do [
    .context_access set _ import _function.context_access,
    function which _ context_access .
]
-----
_free
=====
access const
-----
_ do [
    .context_access set _ import _function.context_access,
    get which _ context_access .
]
-----
_constant
=====
access mutable
-----
_ do [
    .context_access set _ import _function.context_access,
    code which _ context_access .
]
-----
_mutable
=====
access default
-----
_ do [
    .context_access set _ import _function.context_access,
    .f set _ function {},
    f which _ context_access .
]
-----
_mutable
=====
raw_input true
-----
_ do [
    .raw_input set _ import _function.raw_input,
    data which _ raw_input .
]
-----
true
=====
raw_input false
-----
_ do [
    .raw_input set _ import _function.raw_input,
    id which _ raw_input .
]
-----
false
=====
is_primitive primitive
-----
_ do [
    .is_primitive set _ import _function.is_primitive,
    not which _ is_primitive .
]
-----
true
=====
is_primitive composite
-----
_ do [
    .is_primitive set _ import _function.is_primitive,
    .f set _ function {},
    f which _ is_primitive .
]
-----
false
=====
id primitive
-----
_ do [
    .id set _ import _function.id,
    not which _ id .
]
-----
_bit.not
=====
id rename
-----
_ do [
    .id set _ import _function.id,
    .neg set not,
    neg which _ id .
]
-----
_bit.not
=====
code
-----
_ do [
    .code set _ import _function.code,
    .f set _ function {
        code : (_ctx : .i) : .i
    },
    f which _ code .
]
-----
(_ctx : i) : i
=====
ctx
-----
_ do [
    .memory.new set _ import _memory.new,
    .function.memory set _ import _function.memory,
    .memo set _ memory.new {
        a : . : 1
    },
    .f set _ function {
        memory : memo
    },
    (f which _ function.memory .) == memo
]
-----
true
=====
apply free
-----
_ do [
    not apply _ not true
]
-----
true
=====
apply const
-----
_ do [
    _a set 1,
    get apply _a
]
-----
1
=====
apply mut
-----
_ do [
    set apply .a : 1,
    a
]
-----
1
=====
apply eval
-----
_ do [
    test apply true : [.a set true] : [.b set true],
    a and b
]
-----
true
=====
recurse
-----
_ do [
    .g set _ function {
        code : .fi : _ form _ do [
            (..f : ..i) = .fi,
            (.i < 1) test [
                0
            ] : [
                .i + .f .f .i - 1
            ]
        ],
        context_access : _free,
    },
    g g 10
]
-----
55

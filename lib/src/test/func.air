recursive call
-----
_ do [
    f = _ function {
        setup : eval@,
        code : (._ : .i) : .i,
    },
    _ f _ not true
]
-----
false
=====
call external func
-----
_ do [
    f = _ function {
        setup : eval@,
        code : (._ : .i) : _ not .i
    },
    _ f _ not true
]
-----
true
=====
call internal func
-----
_ do [
    context = _ import .context.new,
    f = _ function {
        setup : eval@,
        code : (._ : .i) : _ .not .i,
        context : _ context {
            not : . : not
        },
    },
    _ f _ not true
]
-----
true
=====
call rename
-----
_ do [
    context = _ import .context.new,
    f = _ function {
        setup : eval@,
        code : (._ : .a) : _ .n .a,
        context : _ context {
            n : . : not
        },
    },
    _ f _ not true
]
-----
true
=====
stateful
-----
_ do [
    context = _ import .context.new,
    new_link = _ import .link.new,
    link_get = _ import .link.get,
    link_set = _ import .link.set,
    f = _ function {
        context_access : .free,
        setup : eval@,
        code : ._ : _ do [
            .i = .a link_get _,
            .a link_set .i + 1,
            .i
        ],
        context : _ context {
            a : . : _ new_link 0
        },
    },
    [_ f ., _ f ., _ f .]
]
-----
[0, 1, 2]
=====
multiple stateful
-----
_ do [
    context = _ import .context.new,
    new_link = _ import .link.new,
    link_get = _ import .link.get,
    link_set = _ import .link.set,
    f = _ form _ function {
        context_access : .free,
        setup : eval@,
        code : ._ : _ do [
            .i = .a link_get _,
            .a link_set .i + 1,
            .i
        ],
        context : _ context {
            a : . : _ new_link 0
        },
    },
    f1 = $f,
    f2 = $f,
    [_ f1 ., _ f1 ., _ f1 ., _ f2 ., _ f2 ., _ f2 .]
]
-----
[0, 1, 2, 0, 1, 2]
=====
const const
-----
_ do [
    read = _ import .context.read,
    a = 1,
    f = _ function {
        code : (.c : ._) : .c which _ read ..a,
        context_access : .constant,
    },
    _ f .
]
-----
1
=====
mutable const
-----
_ do [
    read = _ import .context.read,
    a = 1,
    f = _ function {
        code : (.c : ._) : .c which _ read ..a,
        context_access : .mutable,
    },
    _ f .
]
-----
1
=====
mutable mutate
-----
_ do [
    f = _ function {
        code : (.c : .i) : .c which .@i = 2,
        context_access : .mutable,
    },
    a = 1,
    _ f a,
    a
]
-----
2
=====
const mutate fail
-----
_ do [
    f = _ function {
        code : (.c : .i) : .c which .@i = 2,
        context_access : .constant,
    },
    a = 1,
    _ f a,
    a
]
-----
1
=====
const free
-----
_ do [
    a = .,
    f = _ function {
        code : (._c : ._) : 1,
        context_access : .constant,
    },
    a which _ f .
]
-----
1
=====
ctx name occupied
-----
_ do [
    context = _ import .context.new,
    a = 1,
    f = _ function {
        code : (.ctx : ._) : 2,
        context_access : .constant,
        context : _ context {
            ctx : constant : 3
        },
    },
    _ f .,
    a == 1
]
-----
true
=====
ctx explicit
-----
_ do [
    set_first = _ import .pair.set_first,
    a = 1 : 2,
    f = _ function {
        setup : _ mode {
            pair : {} : {symbol : ..} : eval@
        },
        code : (.p : .i) : .p which _ set_first .i,
    },
    a which _ f 3,
    a
]
-----
3 : 2
=====
repr
-----
_ do [
    represent = _ import .function.represent,
    _ represent _ function {
        code : (._ : .i) : .i
    }
]

-----
{code : (_ : i) : i}
=====
repr primitive free
-----
_ do [
    represent = _ import .function.represent,
    _ represent not
]
-----
{id : bit.not}
=====
repr const
-----
_ do [
    represent = _ import .function.represent,
    _ represent _ function {
        code : (._ : .i) : .i,
        context_access : .constant
    }
]
-----
{code : (_ : i) : i, context_access : constant}
=====
repr primitive const
-----
_ do [
    represent = _ import .function.represent,
    _ represent ==
]
-----
{id : ==}
=====
repr composite
-----
_ do [
    represent = _ import .function.represent,
    m = _ mode {
        pair : {} : eval@ : .
    },
    repr = _ form. {
        id : f,
        code : (ctx : in) : in,
        setup : @m,
        context_access : constant,
    },
    new_repr = _ represent _ function repr,
    repr == new_repr
]
-----
true
=====
access free
-----
_ do [
    context_access = _ import .function.context_access,
    function which _ context_access .
]
-----
free
=====
access const
-----
_ do [
    context_access = _ import .function.context_access,
    read = _ import .context.read,
    read which _ context_access .
]
-----
constant
=====
access mutable
-----
_ do [
    context_access = _ import .function.context_access,
    eval@ which _ context_access .
]
-----
mutable
=====
access default
-----
_ do [
    context_access = _ import .function.context_access,
    f = _ function {},
    f which _ context_access .
]
-----
mutable
=====
call setup primitive
-----
_ do [
    setup = _ import .function.setup,
    eval@ == not which _ setup .
]
-----
true
=====
call setup mode
-----
_ do [
    setup = _ import .function.setup,
    . == eval@ which _ setup .
]
-----
true
=====
call setup 1
-----
_ do [
    setup = _ import .function.setup,
    f = _ function {
        setup : _ mode {
            pair : {} : eval@ : form.
        },
    },
    p1 = f which _ setup .,
    p2 = _ mode {
        pair : {} : eval@ : form.
    },
    p1 == p2
]
-----
true
=====
call setup 2
-----
_ do [
    setup = _ import .function.setup,
    f = _ function {
        setup : _ mode {
            list : [eval@, form., .] : form.
        },
    },
    p1 = f which _ setup .,
    p2 = _ mode {
        list : [eval@, form., .] : form.
    },
    p1 == p2
]
-----
true
=====
call setup 3
-----
_ do [
    setup = _ import .function.setup,
    f = _ function {
        setup : _ mode {
            map : {a : eval@, b : .} : . : form.
        },
    },
    p1 = f which _ setup .,
    p2 = _ mode {
        map : {a : eval@, b : .} : . : form.
    },
    p1 == p2
]
-----
true
=====
is_primitive primitive
-----
_ do [
    is_primitive = _ import .function.is_primitive,
    not which _ is_primitive .
]
-----
true
=====
is_primitive composite
-----
_ do [
    is_primitive = _ import .function.is_primitive,
    f = _ function {},
    f which _ is_primitive .
]
-----
false
=====
id primitive
-----
_ do [
    id = _ import .function.id,
    not which _ id .
]
-----
bit.not
=====
id rename
-----
_ do [
    id = _ import .function.id,
    neg = not,
    neg which _ id .
]
-----
bit.not
=====
code
-----
_ do [
    code = _ import .function.code,
    f = _ function {
        code : (.ctx : .i) : .i
    },
    f which _ code .
]
-----
(ctx : i) : i
=====
ctx
-----
_ do [
    context.new = _ import .context.new,
    function.context = _ import .function.context,
    ctx = _ context.new {
        a : . : 1
    },
    f = _ function {
        context : ctx
    },
    (f which _ function.context .) == ctx
]
-----
true

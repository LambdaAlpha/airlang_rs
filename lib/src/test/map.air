length set
-----
_ do [
    .any set _ import _value.any,
    .length set _ import _map.length,
    .map.set set _ import _map.set,
    .contain set _ import _map.contain,
    .a set _ any _map,
    .l1 set a which _ length .,
    .k set _ any _key,
    .v set _ any .,
    .i set (a which _ contain k) test [0] : [1],
    a which _ map.set k : v,
    .l2 set a which _ length .,
    l2 == l1 + i
]
-----
true
=====
length set_many
-----
_ do [
    .any set _ import _value.any,
    .length set _ import _map.length,
    .set_many set _ import _map.set_many,
    .a set _ any _map,
    .la set a which _ length .,
    .b set _ any _map,
    .lb set b which _ length .,
    .c set a which _ set_many b,
    .lc set c which _ length .,
    .lab set a which _ length .,
    lab == la + lb - lc
]
-----
true
=====
items new
-----
_ do [
    .any set _ import _value.any,
    .new set _ import _map.new,
    .items set _ import _map.items,
    .a1 set _ any _map,
    .a2 set _ new a1 which _ items .,
    a1 == a2
]
-----
true
=====
into_items new
-----
_ do [
    .any set _ import _value.any,
    .new set _ import _map.new,
    .into_items set _ import _map.into_items,
    .a1 set _ any _map,
    .a2 set a1,
    .a3 set _ new a1 which _ into_items .,
    (a1 == {}) and (a2 == a3)
]
-----
true
=====
keys set
-----
_ do [
    .any set _ import _value.any,
    .new_set set _ import _map.new_set,
    .keys set _ import _map.keys,
    .set_many set _ import _map.set_many,
    .a1 set _ any _map,
    .a2 set _ new_set a1 which _ keys .,
    a1 which _ set_many a2,
    a1 == a2
]
-----
true
=====
into_keys set
-----
_ do [
    .any set _ import _value.any,
    .new_set set _ import _map.new_set,
    .keys set _ import _map.keys,
    .into_keys set _ import _map.into_keys,
    .a1 set _ any _map,
    .a2 set _ new_set a1 which _ keys .,
    .a3 set _ new_set a1 which _ into_keys .,
    (a1 == {}) and (a2 == a3)
]
-----
true
=====
set contain
-----
_ do [
    .any set _ import _value.any,
    .map.set set _ import _map.set,
    .contain set _ import _map.contain,
    .a set _ any _map,
    .k set _ any _key,
    .v set _ any .,
    a which _ map.set k : v,
    a which _ contain k
]
-----
true
=====
set get
-----
_ do [
    .any set _ import _value.any,
    .map.set set _ import _map.set,
    .map.get set _ import _map.get,
    .a set _ any _map,
    .k set _ any _key,
    .v set _ any .,
    a which _ map.set k : v,
    .v1 set a which _ map.get k,
    v1 == v
]
-----
true
=====
set_many contain_all
-----
_ do [
    .any set _ import _value.any,
    .set_many set _ import _map.set_many,
    .contain_all set _ import _map.contain_all,
    .keys set _ import _map.keys,
    .a set _ any _map,
    .b set _ any _map,
    a which _ set_many b,
    a which _ contain_all b which _ keys .
]
-----
true
=====
remove_many contain_any
-----
_ do [
    .any set _ import _value.any,
    .remove_many set _ import _map.remove_many,
    .contain_any set _ import _map.contain_any,
    .push set _ import _list.push,
    .a set _ any _map,
    .ks set [],
    10 iterate i : [
        ks which _ push _ any _key,
    ],
    a which _ remove_many ks,
    _ not a which _ contain_any ks
]
-----
true
=====
set_many get_many
-----
_ do [
    .any set _ import _value.any,
    .set_many set _ import _map.set_many,
    .get_many set _ import _map.get_many,
    .keys set _ import _map.keys,
    .a set _ any _map,
    .b set _ any _map,
    a which _ set_many b,
    .b1 set a which _ get_many b which _ keys .,
    b == b1
]
-----
true
=====
get remove
-----
_ do [
    .any set _ import _value.any,
    .get set _ import _map.get,
    .remove set _ import _map.remove,
    .contain set _ import _map.contain,
    .a set _ any _map,
    .k set _ any _key,
    .v1 set a which _ get k,
    .v2 set a which _ remove k,
    .c set a which _ contain k,
    (_ not c) and (v1 == v2)
]
-----
true
=====
get_many remove_many
-----
_ do [
    .any set _ import _value.any,
    .get_many set _ import _map.get_many,
    .remove_many set _ import _map.remove_many,
    .contain_any set _ import _map.contain_any,
    .push set _ import _list.push,
    .a set _ any _map,
    .ks set [],
    10 iterate i : [
        ks which _ push _ any _key,
    ],
    .vs1 set a which _ get_many ks,
    .vs2 set a which _ remove_many ks,
    .c set a which _ contain_any ks,
    (_ not c) and (vs1 == vs2)
]
-----
true
=====
clear
-----
_ do [
    .any set _ import _value.any,
    .clear set _ import _map.clear,
    .a set _ any _map,
    a which _ clear .,
    a == {}
]
-----
true
=====
length
-----
_ do [
    .length set _ import _map.length,
    .m set {a : 1, b : 2},
    m which _ length .
]
-----
2
=====
items new 1
-----
_ do [
    .items set _ import _map.items,
    .new set _ import _map.new,
    .m set {a : 1, b : 2},
    .i set m which _ items .,
    _ new i
]
-----
{a : 1, b : 2}
=====
keys new_set 1
-----
_ do [
    .keys set _ import _map.keys,
    .new_set set _ import _map.new_set,
    .m set {a : 1, b : 2},
    .k set m which _ keys .,
    _ new_set k
]
-----
{a, b}
=====
contain 1
-----
_ do [
    .contain set _ import _map.contain,
    .m set {a : .b},
    m which _ contain .a
]
-----
true
=====
contain 2
-----
_ do [
    .contain set _ import _map.contain,
    .m set {a : .b},
    m which _ contain .b
]
-----
false
=====
contain_all 1
-----
_ do [
    .contain_all set _ import _map.contain_all,
    .m set {a : 1, b : 2},
    m which _ contain_all [.a, .b]
]
-----
true
=====
contain_all 2
-----
_ do [
    .contain_all set _ import _map.contain_all,
    .m set {a : .b, c : .d},
    m which _ contain_all [.a, .b]
]
-----
false
=====
contain_any 1
-----
_ do [
    .contain_any set _ import _map.contain_any,
    .m set {a : .b, c : .d},
    m which _ contain_any [.a, .b]
]
-----
true
=====
contain_any 2
-----
_ do [
    .contain_any set _ import _map.contain_any,
    .m set {a : .b, c : .d},
    m which _ contain_any [.b, .d]
]
-----
false
=====
set 1
-----
_ do [
    .map.set set _ import _map.set,
    .m set {a : 1, b : 2},
    [m which _ map.set .a : 3, m]
]
-----
[1, {a : 3, b : 2}]
=====
set 2
-----
_ do [
    .map.set set _ import _map.set,
    .m set {a : 1},
    [m which _ map.set .b : 2, m]
]
-----
[., {a : 1, b : 2}]
=====
set_many 1
-----
_ do [
    .set_many set _ import _map.set_many,
    .m set {a : 1, b : 2},
    [m which _ set_many {a : 3, c : 4}, m]
]
-----
[{a : 1}, {a : 3, b : 2, c : 4}]
=====
get 1
-----
_ do [
    .map.get set _ import _map.get,
    .m set {a : 2},
    m which _ map.get .a
]
-----
2
=====
get 2
-----
_ do [
    .map.get set _ import _map.get,
    .m set {a : .b},
    m which _ map.get .b
]
-----
.
=====
get_many duplicate keys
-----
_ do [
    .get_many set _ import _map.get_many,
    .m set {a : 1, b : 2, c : 3},
    m which _ get_many [.a, .b, .a, .d]
]
-----
{a : 1, b : 2}
=====
remove 1
-----
_ do [
    .remove set _ import _map.remove,
    .m set {a : 1, b : 2},
    [m which _ remove .a, m]
]
-----
[1, {b : 2}]
=====
remove 2
-----
_ do [
    .remove set _ import _map.remove,
    .m set {a : .b},
    [m which _ remove .b, m]
]
-----
[., {a : b}]
=====
remove_many 1
-----
_ do [
    .remove_many set _ import _map.remove_many,
    .m set {a : 1, b : 2, c : 3},
    [m which _ remove_many [.a, .c, .e], m]
]
-----
[{a : 1, c : 3}, {b : 2}] 
=====
clear 1
-----
_ do [
    .clear set _ import _map.clear,
    .m set {a : 1, b : 2},
    m which _ clear .,
    m
]
-----
{}
=====
new 1
-----
_ do [
    .new set _ import _map.new,
    _ new [.a : 1, .b : 2]
]
-----
{a : 1, b : 2}
=====
map duplicate keys
-----
_ do [
    .new set _ import _map.new,
    _ new [.a : 1, .a : 2]
]
-----
{a : 2}
=====
new_set duplicate keys 1
-----
_ do [
    .new_set set _ import _map.new_set,
    _ new_set [.a, .b, .b, .c]
]
-----
{a, b, c}
=====
set duplicate keys 2
-----
_ do [
    .new_set set _ import _map.new_set,
    _ new_set [.a, .b, .a]
]
-----
{a, b}
